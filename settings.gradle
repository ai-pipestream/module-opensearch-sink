pluginManagement {
    repositories {
        gradlePluginPortal()
        // Ensure plugins can also be resolved from Maven Central (Gradle 9+ recommendation)
        mavenCentral()
    }
}

rootProject.name = 'module-opensearch-sink'

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            // Import version catalog from published BOM
            from("ai.pipestream:pipeline-bom-catalog:0.2.2")
        }
    }

    repositories {
        // Maven Local for published Pipeline artifacts during development
        mavenLocal {
            content {
                includeGroupByRegex "ai\\.pipestream(\\..*)?"
            }
        }

        mavenCentral()

        // Maven Central Snapshots for consuming published SNAPSHOTs
        maven {
            url = uri('https://central.sonatype.com/repository/maven-snapshots/')
            mavenContent {
                snapshotsOnly()
            }
        }

        // Apache snapshots for Tika 4.x pre-release (default for public builds)
        if (System.getenv('CI_ENV') != 'internal') {
            maven {
                url = uri('https://repository.apache.org/content/repositories/snapshots/')
                allowInsecureProtocol = false
                content {
                    includeGroupByRegex "org\\.apache\\.tika(\\..*)?"
                }
            }
        }
    }
}
